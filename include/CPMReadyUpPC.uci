/*
 * Player Controller functions for the ready up system
 */

exec function Ready()
{
	if (!IsVoluntarySpectator())
		ServerReady();
}

reliable server function ServerReady()
{
	ICPMReadyUpGame(WorldInfo.Game).ReadyUp(CurrentFamilyInfo.default.FamilyFaction);
}

exec function Unready()
{
	if (!IsVoluntarySpectator())
		ServerUnready();
}

reliable server function ServerUnready()
{
	ICPMReadyUpGame(WorldInfo.Game).UnreadyUp(CurrentFamilyInfo.default.FamilyFaction);
}

exec function AdminForceReady()
{
	ServerForceReady();
}

reliable server function ServerForceReady()
{
	if (PlayerReplicationInfo.bAdmin || WorldInfo.NetMode == NM_Standalone)
		ICPMReadyUpGame(WorldInfo.Game).ForceReady();
}

exec function AdminForceUnready()
{
	ServerForceUnready();
}

reliable server function ServerForceUnready()
{
	if (PlayerReplicationInfo.bAdmin || WorldInfo.NetMode == NM_Standalone)
		ICPMReadyUpGame(WorldInfo.Game).ForceUnready();
}

reliable client function ShowReadyUpHeader(bool bShow)
{
	AOCBaseHUD(myHUD).HUD.MainHud.ShowLargeHeaderText(bShow, "Both teams must type 'Ready' in console to begin.");
}

// ADVANCED PAUSING //
exec function Pause()
{
	ServerPause();
}

reliable server function ServerPause()
{
	if( !IsPaused() )
		SetPause(true);
	else
		ClientDisplayConsoleMessage("Server is already paused. Use Unpause to resume play.");
}

exec function Unpause()
{
	ServerUnpause();
}

reliable server function ServerUnpause()
{
	if( IsPaused() )
		SetPause(false);
	else
		ClientDisplayConsoleMessage("Server is not paused.");
}

function bool SetPause( bool bPause, optional delegate<CanUnpause> CanUnpauseDelegate=CanUnpause)
{
	local bool bResult;

	if (bPause)
	{
		bFire = 0;
		// Pause gamepad rumbling too if needed
		bResult = WorldInfo.Game.SetPause(self,CanUnpauseDelegate);
		if (bResult)
		{
			PauseRumbleForAllPlayers();
		}
	}
	else
	{
		ClientDisplayConsoleMessage("Pause Mode is"@ICPMGame(WorldInfo.Game).GetPauseMode());
		if((PlayerReplicationInfo.bAdmin || WorldInfo.NetMode == NM_Standalone) && ICPMGame(WorldInfo.Game).GetPauseMode() != 3)
		{
			AOCGame(Worldinfo.Game).BroadcastMessage(none, "Admin " $ PlayerReplicationInfo.PlayerName $ " has unpaused the game.", EFAC_ALL, true, true, "#DF7401");
		}
		else if(ICPMGame(WorldInfo.Game).GetPauseMode() == 2 && !IsVoluntarySpectator())
		{
			if(CurrentFamilyInfo.default.FamilyFaction == EFAC_Agatha)
				AOCGame(Worldinfo.Game).BroadcastMessage(none, PlayerReplicationInfo.PlayerName $ " for team Agatha has unpaused the game.", EFAC_ALL, true, true, "#DF7401");
			else if (CurrentFamilyInfo.default.FamilyFaction == EFAC_Mason)
				AOCGame(Worldinfo.Game).BroadcastMessage(none, PlayerReplicationInfo.PlayerName $ " for team Mason has unpaused the game.", EFAC_ALL, true, true, "#DF7401");
		}
		else
		{
			AOCGame(Worldinfo.Game).BroadcastMessage(none, PlayerReplicationInfo.PlayerName $ " has unpaused the game.", EFAC_ALL, true, true, "#DF7401");
		}

		AOCGame(WorldInfo.Game).ClearPause();
		// If the unpause is complete, let rumble occur
		if ( WorldInfo.Pauser == None )
		{
			// If we did a gameplay frame pause clear it out now
			WorldInfo.bGameplayFramePause = false;

			PauseRumbleForAllPlayers(false);
		}
	}
	return bResult;
}